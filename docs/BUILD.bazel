load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load(
    "@cgrindel_bazel_starlib//bazeldoc:defs.bzl",
    "doc_for_provs",
    "write_header",
    doc_providers = "providers",
)
load("@cgrindel_bazel_starlib//bzlformat:defs.bzl", "bzlformat_pkg")
load("@cgrindel_bazel_starlib//markdown:defs.bzl", "markdown_pkg")
load(":doc_infos.bzl", "doc_infos")

bzlformat_pkg(name = "bzlformat")

markdown_pkg(name = "markdown")

# MARK: - Documentation Declarations

_DOC_INFOS = [
    doc_infos.new(
        name = "internal_rules_and_macros",
        label = "//swiftpkg:build_defs",
        symbols = [
            "generate_modulemap",
            "resource_bundle_accessor",
            "resource_bundle_infoplist",
        ],
    ),
    doc_infos.new(
        name = "repository_rules",
        label = "//swiftpkg:defs",
        symbols = [
            "local_swift_package",
            "swift_package",
            "registry_swift_package",
        ],
    ),
    doc_infos.new(
        name = "rules_and_macros",
        label = "//swiftpkg:defs",
        symbols = [
            "swift_deps_index",
        ],
    ),
    doc_infos.new(
        name = "bzlmod_extensions",
        label = "//:extensions",
        symbols = [
            "swift_deps",
        ],
    ),
]

_DOC_WITH_SYMBOLS = {
    di.name: di
    for di in _DOC_INFOS
}

_ALL_DOC_PROVIDERS = [
    doc_providers.create(
        name = di.name + "_overview",
        stardoc_input = "{}.bzl".format(di.label),
        symbols = di.symbols,
        deps = [di.label],
    )
    for di in _DOC_INFOS
]

# MARK: - Headers

write_header(
    name = "repository_rules_overview_header",
    header_content = [
        "# Repository Rules",
        "",
        """
The rules described below are used to build Swift packages and make their
products and targets available as Bazel targets.
""",
    ],
    symbols = _DOC_WITH_SYMBOLS["repository_rules"].symbols,
)

write_header(
    name = "rules_and_macros_overview_header",
    header_content = [
        "# Rules and Macros",
        "",
        """
The rules and macros described below are used to define Gazelle targets to aid \
in the generation and maintenance of Swift package dependencies.
""",
    ],
    symbols = _DOC_WITH_SYMBOLS["rules_and_macros"].symbols,
)

write_header(
    name = "internal_rules_and_macros_overview_header",
    header_content = [
        "# Internal Rules and Macros",
        "",
        """
The rules and macros described below are used by `rules_swift_package_manager` to build the \
external Swift packages.
""",
    ],
    symbols = _DOC_WITH_SYMBOLS["internal_rules_and_macros"].symbols,
)

write_header(
    name = "bzlmod_extensions_overview_header",
    header_content = [
        "# Bazel Modules (bzlmod) Extensions",
        "",
        """
The bzlmod extensions described below are used by `rules_swift_package_manager` \
to customize the build of the external Swift packages.\
""",
    ],
    symbols = _DOC_WITH_SYMBOLS["bzlmod_extensions"].symbols,
)

doc_for_provs(doc_provs = _ALL_DOC_PROVIDERS)

bzl_library(
    name = "doc_infos",
    srcs = ["doc_infos.bzl"],
    visibility = ["//visibility:public"],
)
