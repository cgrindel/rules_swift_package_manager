"""Utilities for working with `Package.swift` files."""

def _package_dependency_identity_str(dep):
    if dep.source_control and dep.source_control.pin:
        return "url: \"{location}\"".format(location = dep.source_control.pin.location)
    elif dep.registry and dep.registry.pin:
        return "id: \"{id}\"".format(id = dep.registry.pin.identity)
    else:
        fail("Unexpected dependency: {dep}".format(dep = dep))

def _package_dependency_str(mod_name, dep):
    requirement = dep.requirement
    if not requirement:
        fail("Expected a requirement for {name} defined package dependency: {dep}".format(
            name = mod_name,
            deps = dep,
        ))

    identity = _package_dependency_identity_str(dep)
    if requirement.exact:
        return """\
        .package({identity}, exact: \"{exact}\")\
""".format(identity = identity, exact = requirement.exact)
    elif requirement.branch:
        return """\
        .package({identity}, branch: "{branch}")\
""".format(identity = identity, branch = requirement.branch)
    elif requirement.revision:
        return """\
        .package({identity}, revision: "{revision}")\
""".format(identity = identity, revision = requirement.revision)
    elif requirement.lower_bound and requirement.upper_bound:
        return """\
        .package({identity}, "{lb}"..<"{ub}")\
""".format(identity = identity, lb = requirement.lower_bound, ub = requirement.upper_bound)
    else:
        fail("Unexpected requirement for {name} defined package dependency: {dep}".format(
            name = mod_name,
            dep = dep,
        ))

def _create_merged_package_swift(*, all_deps_by_mod, tools_version):
    """Creates a merged `Package.swift` file from the specified `Package.swift` files.

    The "merged" Package.swift file simply contains a local `package(path:)` usage for \
    each of the specified `Package.swift` files.

    Args:
        all_deps_by_mod: A `dict` of module name to `list` of `struct` as \
            returned by `pkginfos.dependency` objects.
        tools_version: The Swift tools version used by the root module.

    Returns:
        A `string` representing the merged `Package.swift` file content.
    """

    dependencies = []
    for mod_name, deps in all_deps_by_mod.items():
        for dep in deps:
            dependencies.append(_package_dependency_str(mod_name, dep))

    # sort contents for reproducibility
    dependencies = sorted(dependencies)

    content = """
// swift-tools-version: {tools_version}

// NOTE: THIS FILE IS AUTO-GENERATED BY rules_swift_package_manager. DO NOT EDIT.

import PackageDescription

let package = Package(
    name: "merged",
    dependencies: [
{dependencies_str}
    ]
)
    """.format(
        tools_version = tools_version,
        dependencies_str = ",\n".join(dependencies),
    )

    return content

swift_package_files = struct(
    merge = _create_merged_package_swift,
)
