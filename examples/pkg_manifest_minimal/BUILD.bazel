load("@bazel_gazelle//:def.bzl", "gazelle", "gazelle_binary")
load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@cgrindel_bazel_starlib//bzltidy:defs.bzl", "tidy")

tidy(
    name = "tidy",
    targets = [
        ":swift_update_repos",
        ":update_build_files",
    ],
)

# MARK: - Gazelle

# Ignore the Swift build folder
# gazelle:exclude .build

gazelle_binary(
    name = "gazelle_bin",
    languages = [
        "@bazel_skylib//gazelle/bzl",
        "@cgrindel_swift_bazel//gazelle",
    ],
)

gazelle(
    name = "update_build_files",
    gazelle = ":gazelle_bin",
)

_UPDATE_REPOS_ARGS = [
    "-from_file=Package.swift",
    "-to_macro=swift_deps.bzl%swift_dependencies",
    "-prune",
]

gazelle(
    name = "swift_update_repos",
    args = _UPDATE_REPOS_ARGS,
    command = "update-repos",
    gazelle = ":gazelle_bin",
)

gazelle(
    name = "swift_update_repos_to_latest",
    args = _UPDATE_REPOS_ARGS + [
        "-swift_update_packages_to_latest",
    ],
    command = "update-repos",
    gazelle = ":gazelle_bin",
)

bzl_library(
    name = "swift_deps",
    srcs = ["swift_deps.bzl"],
    visibility = ["//visibility:public"],
    deps = ["@cgrindel_swift_bazel//swiftpkg:defs"],
)

alias(
    name = "swiftformat",
    # This is the executable product.
    # FYI. The identity for SwiftFormat in the swift package dump-package is
    # lowercase. That is why it is lowercase here.
    actual = "@swiftpkg_swiftformat//:swiftformat",
)
