load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@bazel_skylib//lib:selects.bzl", "selects")
load("@cgrindel_bazel_starlib//bzlformat:defs.bzl", "bzlformat_pkg")
load(":platforms.bzl", "platforms")

package(default_visibility = ["//visibility:public"])

bzlformat_pkg(name = "bzlformat")

# NOTE: Ensure that the list of platforms in
# //config_settings/spm/platform/platforms.bzl stays in sync with the
# config_setting and selects.config_setting_group declarations.

# MARK: - Platform

[
    selects.config_setting_group(
        name = pi.spm,
        match_any = [
            # Removed the apple_platform_type check, because it can evaluate
            # to macos on Linux.
            # "//config_settings/bazel/apple_platform_type:{}".format(pi.bzl),
            "@platforms//os:{}".format(pi.os),
        ],
    )
    for pi in platforms.all_platform_infos
    if pi.bzl != None and pi.os != None
]

[
    config_setting(
        name = pi.spm,
        constraint_values = [
            "@platforms//os:{}".format(pi.os),
        ],
    )
    for pi in platforms.all_platform_infos
    if pi.bzl == None and pi.os != None
]

filegroup(
    name = "all_files",
    srcs = glob(["*"]),
    visibility = ["//:__subpackages__"],
)

bzl_library(
    name = "platforms",
    srcs = ["platforms.bzl"],
    deps = ["//config_settings/bazel/apple_platform_type:apple_platform_types"],
)
