load("@bazel_gazelle//:def.bzl", "gazelle_binary", "gazelle_generation_test")
load("@cgrindel_bazel_starlib//bzlformat:defs.bzl", "bzlformat_pkg")

bzlformat_pkg(name = "bzlformat")

# Exclude this entire directory from having anything gnerated by Gazelle. That
# way the test cases won't be fixed by `bazel run //:gazelle` when run in this
# repository.
# gazelle:exclude **

gazelle_binary(
    name = "gazelle_with_swift",
    languages = [
        "//gazelle",
    ],
    visibility = ["//visibility:private"],
)

[gazelle_generation_test(
    # Name the test the path to the directory containing the WORKSPACE file.
    name = file[0:-len("/WORKSPACE")],
    gazelle_binary = ":gazelle_with_swift",
    test_data = glob(
        include = [file[0:-len("/WORKSPACE")] + "/**"],
    ),
) for file in glob(
    include = ["**/WORKSPACE"],
)]
